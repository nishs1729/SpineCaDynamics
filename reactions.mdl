// Bartol et al. 2015, Frontiers in Synaptic Neuroscience

DEFINE_REACTIONS
{
  // Immobile Ca Binding Protein Reactions
  ca + immo_cbp -> ca.immo_cbp [>2.47e+08] : Immo_CBP
  ca.immo_cbp -> ca + immo_cbp [>524]

  // Calbindin Reactions
  ca + calb -> ca.calb.m1 [>1.74e+08] : CalbM1
  ca.calb.m1 -> ca +calb [>35.8]
  ca + ca.calb.h1 -> ca.calb.m1.h1 [>1.74e+08] : CalbM1H1_1
  ca.calb.m1.h1 -> ca + ca.calb.h1 [>35.8]
  ca + ca.calb.h2 -> ca.calb.m1.h2 [>1.74e+08] : CalbM1H2_1
  ca.calb.m1.h2 -> ca + ca.calb.h2 [>35.8]
  ca + ca.calb.m1 -> ca.calb.m2 [>8.7e+07] : CalbM2
  ca.calb.m2 -> ca + ca.calb.m1 [>71.6]
  ca + ca.calb.m1.h1 -> ca.calb.m2.h1 [>8.7e+07] : CalbM2H1_1
  ca.calb.m2.h1 -> ca + ca.calb.m1.h1 [>71.6]
  ca + ca.calb.m1.h2 -> ca.calb.m2.h2 [>8.7e+07] : CalbM2H2_1
  ca.calb.m2.h2 -> ca + ca.calb.m1.h2 [>71.6]
  ca + calb -> ca.calb.h1 [>2.2e+07] : CalbH1
  ca.calb.h1 -> ca + calb [>2.6]
  ca + ca.calb.m1 -> ca.calb.m1.h1 [>2.2e+07] : CalbM1H1_2
  ca.calb.m1.h1 -> ca + ca.calb.m1 [>2.6]
  ca + ca.calb.m2 -> ca.calb.m2.h1 [>2.2e+07] : CalbM2H1_2
  ca.calb.m2.h1 -> ca + ca.calb.m2 [>2.6]
  ca + ca.calb.h1 -> ca.calb.h2 [>1.1e+07] : CalbH2
  ca.calb.h2 -> ca + ca.calb.h1 [>5.2]
  ca + ca.calb.m1.h1 -> ca.calb.m1.h2 [>1.1e+07] : CalbM1H2_2
  ca.calb.m1.h2 -> ca + ca.calb.m1.h1 [>5.2]
  ca + ca.calb.m2.h1 -> ca.calb.m2.h2 [>1.1e+07] : CalbM2H2_2
  ca.calb.m2.h2 -> ca + ca.calb.m2.h1 [>5.2]

  
  // Calmodulin Reactions
  /*ca + CaM0 <-> CaM1C [>6.8e+06, <68]
  ca + CaM1N <-> CaM1N1C [>6.8e+06, <68]
  ca + CaM2N <-> CaM2N1C [>6.8e+06, <68]
  ca + CaM1C <-> CaM2C [>6.8e+06, <10]
  ca + CaM1N1C <-> CaM1N2C [>6.8e+06, <10]
  ca + CaM2N1C <-> CaM4 [>6.8e+06, <10]
  ca + CaM0 <-> CaM1N [>1.08e+08, <4150]
  ca + CaM1C <-> CaM1N1C [>1.08e+08, <4150]
  ca + CaM2C <-> CaM1N2C [>1.08e+08, <4150]
  ca + CaM1N <-> CaM2N [>1.08e+08, <800]
  ca + CaM1N1C <-> CaM2N1C [>1.08e+08, <800]
  ca + CaM1N2C <-> CaM4 [>1.08e+08, <800]*/

  // PMCA Reactions
  pmca' + ca, -> ca.pmca' [1.5e+08]
  ca.pmca' -> pmca' + ca' [15]
  ca.pmca' -> pmca' [12] : PMCA_Pump
  pmca' -> pmca' + ca, [6.67] : PMCA_Leak

  // NCX Reactions
  ncx' + ca' -> ca.ncx' [3e+08]
  ca.ncx' -> ncx' + ca' [300]
  ca.ncx' -> ncx' [600] : NCX_Pump
  ncx' -> ncx' + ca, [20] : NCX_Leak

  // VDCC Reactions
  vdcc_c0' -> vdcc_c1' ["vdcc_alpha_1_BAP.txt"]
  vdcc_c1' -> vdcc_c0' ["vdcc_beta_1_BAP.txt"]
  vdcc_c1' -> vdcc_c2' ["vdcc_alpha_2_BAP.txt"]
  vdcc_c2' -> vdcc_c1' ["vdcc_beta_2_BAP.txt"]
  vdcc_c2' -> vdcc_c3' ["vdcc_alpha_3_BAP.txt"]
  vdcc_c3' -> vdcc_c2' ["vdcc_beta_3_BAP.txt"]
  vdcc_c3' -> vdcc_open' ["vdcc_alpha_4_BAP.txt"]
  vdcc_open' -> vdcc_c3' ["vdcc_beta_4_BAP.txt"]
  vdcc_open' -> vdcc_open' + ca, ["vdcc_kca_BAP.txt"] : VDCC_Influx

  // NMDA Reactions
  glu; + nmdar_c0b' -> nmdar_c1b' [2e+07]
  nmdar_c1b' -> nmdar_c0b' + glu' [11]
  glu; + nmdar_c1b' -> nmdar_c2b' [1e+07]
  nmdar_c2b' -> nmdar_c1b' + glu' [22]
  nmdar_c2b' -> nmdar_db' [16.8]
  nmdar_db' -> nmdar_c2b' [3.6]
  nmdar_c2b' -> nmdar_ob' [97]
  nmdar_ob' -> nmdar_c2b' [574]
  nmdar_ob' -> nmdar_open' ["nmdar_kU_BAP.txt"]
  nmdar_open' -> nmdar_ob' ["nmdar_kB_BAP.txt"]
  nmdar_open' -> nmdar_open' + ca, ["nmdar_kca_BAP.txt"] : NMDAR_Influx
  nmdar_open' -> nmdar_c2' [183.2]
  nmdar_c2' -> nmdar_open' [93]
  nmdar_c2' -> nmdar_d' [16.8]
  nmdar_d' -> nmdar_c2' [3.6]
  nmdar_c2' -> nmdar_c1' + glu' [22]
  glu; + nmdar_c1' -> nmdar_c2' [1e+07]
  nmdar_c1' -> nmdar_c0' + glu' [11]
  glu; + nmdar_c0' -> nmdar_c1' [2e+07]

  // Glutamate Degradataion
  glu -> NULL [1000]
}

